// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:

   //handling zx,nx
   Mux16(a=x, b=false, sel=zx, out=px1);
   Not16(in=px1, out=npx1);
   Mux16(a=px1, b=npx1, sel=nx, out=px2);

   //handling zy,ny
   Mux16(a=y, b=false, sel=zy, out=py1);
   Not16(in=py1, out=npy1);
   Mux16(a=py1, b=npy1, sel=ny, out=py2);

   //handling f
   And16(a=px2, b=py2, out=xandy);
   Add16(a=px2, b=py2, out=xplusy);

   Mux16(a=xandy, b=xplusy, sel=f, out=pxy);
   Not16(in=pxy, out=npxy);

   //handling no
   Mux16(a=pxy, b=npxy, sel=no, out[0..7]=out1,out[8..14]= out2,out[15] =out3);

   //handling zr,ng,out
   Or8Way(in=out1, out=temp1);
   Or8Way(in[0..6]=out2,in[7]=out3, out=temp2);
   Or(a=temp1, b=temp2, out=temp3);

   Not(in=temp3, out=zr);
   And(a=out3, b=true, out=ng);
   And16(a[0..7]=out1,a[8..14]=out2,a[15]=out3, b=true, out=out);
}